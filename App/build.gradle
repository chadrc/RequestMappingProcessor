
group 'com.chadrc.annotations.processors'
version '1.0'

buildscript {
    ext {
        springBootVersion = '1.4.4.RELEASE'
    }
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'maven-publish'

jar {
    baseName = 'testapi'
    version = '1.0'
}

sourceCompatibility = 1.8

sourceSets {
    api
    shared
}

task apiJar(type: Jar) {
    from sourceSets.api.output, sourceSets.shared.output
}

publishing {
    publications {
        mavenApi(MavenPublication) {
            artifactId = 'testapi'
            groupId = group
            version = version

            pom.withXml {
                def depRoot = asNode().appendNode("dependencies")
                configurations.apiCompile.dependencies.each {
                    if (it.group == null || it.name == null || it.version == null) {
                        return
                    }
                    def dep = depRoot.appendNode("dependency")
                    dep.appendNode("groupId").setValue(it.group)
                    dep.appendNode("artifactId").setValue(it.name)
                    dep.appendNode("version", it.version)
                }
            }

            artifact apiJar
        }
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compileOnly group: "com.chadrc.annotations.processors", name: "RequestMappingProcessor", version: "1.0"
    compile('org.springframework.boot:spring-boot-starter-web')
    compile sourceSets.shared.output

    apiCompile group: "com.chadrc.services.api", name: "base-api", version: "1.0"
    apiCompile sourceSets.shared.output
}

task deleteApi(type: Delete) {
    delete "src/api"
}

clean.dependsOn deleteApi