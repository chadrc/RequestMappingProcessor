group 'com.chadrc.annotations.processors'
version '1.0'

buildscript {
    ext {
        springBootVersion = '1.4.4.RELEASE'
    }
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'org.springframework.boot'

sourceCompatibility = 1.8

sourceSets {
    baseApi
}

repositories {
    mavenCentral()
}

task baseApiJar(type: Jar) {
    from sourceSets.baseApi.output
}
baseApiJar.dependsOn baseApiClasses

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId "RequestMappingProcessor"
            from components.java
        }

        mavenBaseApi(MavenPublication) {
            artifactId "base-api"
            groupId "com.chadrc.services.api"
            version "1.0"

            pom.withXml {
                def depRoot = asNode().appendNode("dependencies")
                configurations.baseApiCompile.dependencies.each {
                    def dep = depRoot.appendNode("dependency")
                    dep.appendNode("groupId").setValue(it.group)
                    dep.appendNode("artifactId").setValue(it.name)
                    dep.appendNode("version", it.version)
                }
            }
            artifact baseApiJar
        }
    }
}

dependencies {
    compile group: 'org.apache.velocity', name: 'velocity', version: '1.7'
    compile('org.springframework.boot:spring-boot-starter-web')

    testCompile group: 'junit', name: 'junit', version: '4.11'

    baseApiCompile group: 'com.mashape.unirest', name: 'unirest-java', version: '1.4.9'
    baseApiCompile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.6'
}