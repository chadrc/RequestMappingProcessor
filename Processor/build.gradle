group 'com.chadrc.annotations.processors'
version '1.0'

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

sourceSets {
    baseApi
}

repositories {
    mavenCentral()
}

task baseApiJar(type: Jar) {
    baseName "baseApi"
    from sourceSets.baseApi.java
}
baseApiJar.dependsOn baseApiClasses

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }

        mavenBaseApi(MavenPublication) {
            artifactId "base-api"
            groupId "com.chadrc.services.api"
            version "1.0"

            pom.withXml {
                def depRoot = asNode().appendNode("dependencies")
                configurations.baseApiCompile.dependencies.each {
                    def dep = depRoot.appendNode("dependency")
                    dep.appendNode("groupId").setValue(it.group)
                    dep.appendNode("artifactId").setValue(it.name)
                    dep.appendNode("version", it.version)
                    dep.appendNode("type", "jar")
                    dep.appendNode("scope", "compile")
                    dep.appendNode("optional", "false")
                }
            }
            artifact baseApiJar
        }
    }
}

dependencies {
    compile group: 'org.apache.velocity', name: 'velocity', version: '1.7'

    testCompile group: 'junit', name: 'junit', version: '4.11'

    baseApiCompile group: 'com.mashape.unirest', name: 'unirest-java', version: '1.4.9'
    baseApiCompile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.6'
}